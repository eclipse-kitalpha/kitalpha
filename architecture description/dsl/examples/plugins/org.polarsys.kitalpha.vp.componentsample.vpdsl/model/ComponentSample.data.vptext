/*******************************************************************************
 * Copyright (c) 2014, 2020 Thales Global Services S.A.S.
 *  This program and the accompanying materials are made available under the
 *  terms of the Eclipse Public License 2.0 which is available at
 *  http://www.eclipse.org/legal/epl-2.0
 *  
 *  SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *  Thales Global Services S.A.S - initial API and implementation
 ******************************************************************************/
 
 
/**
 *	Viewpoint ComponentSample
 *	@author: Benoît Langlois
 *	@date: 26/08/14
 *
 */
Data ComponentSample.data {
	// Root super-class
	Class ComponentElement {
		superClass external emde.ExtensibleElement
		abstract: true
		Attributes:
			name type ecore.EString
			^description type ecore.EString
	}
	// Common Organization
	Class ComponentModel {
		icon: "model-icon.png"
		superClass ComponentElement
		Associations:
			packages contains [0,*] Package
	}
	Class Package {
		superClass ComponentElement
		abstract: true
	}
	
	// Component elements
	Class ComponentPackage {
		icon: "package-icon.png"
		superClass Package
		Associations:
			components contains[0,*] AbstractComponent
	}
	
	Class AbstractComponent {
		superClass ComponentElement
		abstract: true
		Attributes:
			^type enum ComponentType
		Associations:
			use refers [0,*] AbstractComponent
	}
	Class SoftwareComponent {
		icon: "Software-icon.png"
		superClass AbstractComponent
	}
	Class HardwareComponent {
		icon: "Hardware-icon.png"
		superClass AbstractComponent
		Annotation "http://org.polarsys.kitalpha/documentation" {
			key: ^description value: "Hardware Component"
		}
		Associations:
			abstractComponent contains [0,*] AbstractComponent
	}
	Enumeration ComponentType {
		Behaviour , ^Data , Service
	}
}