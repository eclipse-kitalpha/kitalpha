/*******************************************************************************
 * Copyright (c) 2014, 2020 Thales Global Services S.A.S.
 *  This program and the accompanying materials are made available under the
 *  terms of the Eclipse Public License 2.0 which is available at
 *  http://www.eclipse.org/legal/epl-2.0
 *  
 *  SPDX-License-Identifier: EPL-2.0
 * 
 * Contributors:
 *  Thales Global Services S.A.S - initial API and implementation
 ******************************************************************************/
 /**
 * 
 * @author Amine Lajmi
 * 		   Faycal Abka
 *
 */
grammar org.polarsys.kitalpha.ad.viewpoint.dsl.cs.text.Vpspec with org.polarsys.kitalpha.ad.viewpoint.dsl.cs.text.Common

/**
 * FIXME to remember, Emde nsuri import removed because it is not used
 */
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/vpdesc/1.0.0" as vpdesc
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/vpconf/1.0.0" as vpconf
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/vpbuild/1.0.0" as vpbuild
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/vpui/1.0.0" as vpui
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/vpdiagram/1.0.0" as vpdiagram
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/commondata/1.0.0" as commondata
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/vpdiagram/expression/1.0.0" as expression
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/vpservices/1.0.0" as vpservices
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/cs/text/vpservices/1.0.0" as services
import "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/as/viewpointactivityexplorer/1.0.0" as activtyExplorer

generate vpspec "http://www.polarsys.org/kitalpha/ad/viewpoint/dsl/cs/text/vpspec/1.0.0"

/********************************************************************
 * 							vpspec
 * 
 ********************************************************************/

Viewpoint returns Viewpoint:
	{Viewpoint}
	'Viewpoint'
	(shortName=ID)?
	'{'
		//('id:' id = EString)?
		('name:' name=STRING)
		('description:' description=EString)?
		('extends' parents+=[Viewpoint|FQN] ( ',' parents+=[Viewpoint|FQN])* )?
		('aggregates' dependencies+=[Viewpoint|FQN] ( ',' dependencies+=[Viewpoint|FQN])* )?
		
		//Viewpoint use link
		('uses viewpoint' useViewpoint+=[Viewpoint | FQN] (',' useViewpoint+=[Viewpoint | FQN])*)?
		
		//Viewpoint use resources
		('uses model' useAnyEMFResource+=EString (',' useAnyEMFResource+=EString)*)?
		('uses diagram' useDiagramResource+=EString (',' useDiagramResource+=EString)*)?
		('uses workspace' useWorkspaceResource+=EString (',' useWorkspaceResource+=EString)*)?
		('uses filesystem' useFSResource+=EString (',' useFSResource+=EString)*)?
		
		
		('Data' VP_Data=[vpdesc::Data | FQN])?
		(type+='UI' VP_Aspects+=[vpui::UIDescription | FQN] )?
		(type+='Diagrams' VP_Aspects+=[vpdiagram::DiagramSet | FQN])?
		(type+='Activity-Explorer' VP_Aspects += [activtyExplorer::ViewpointActivityExplorer | FQN])?
		(type+='Services' VP_Aspects+= [vpservices::ServiceSet | FQN])? //[services::Services | FQN])?
		(type+='Build' VP_Aspects+=[vpbuild::Build| FQN])?
		(type+='Configuration' VP_Aspects+=[vpconf::Configuration | FQN])?
	'}'
;

